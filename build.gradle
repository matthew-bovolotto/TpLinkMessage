plugins {
    id 'java'
    id 'application'
    id 'groovy'
    id 'maven'
}

version '1.0.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

startScripts {
    enabled = false
}
run.enabled = false

task createPrintReturnMessage(type: CreateStartScripts){
    mainClassName = 'main.PrintReturnMessage'
    outputDir = startScripts.outputDir
    classpath = startScripts.classpath
    applicationName = 'printReturnMessage'
}

task createLightSwitchServer(type: CreateStartScripts){
    mainClassName = 'main.LightSwitchServer'
    outputDir = startScripts.outputDir
    classpath = startScripts.classpath
    applicationName = 'lightSwitchServer'
}

tasks.withType(CreateStartScripts){
    doLast{
        it.windowsScript.delete()
    }
}

applicationDistribution.into("bin"){
    from(createPrintReturnMessage)
    from(createLightSwitchServer)
    fileMode = 0755
}

dependencies {
    compile group: 'net.sourceforge.argparse4j', name: 'argparse4j', version: '0.8.1'
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'

    // https://mvnrepository.com/artifact/commons-configuration/commons-configuration
    compile group: 'commons-configuration', name: 'commons-configuration', version: '1.7'


    testCompile group: 'junit', name: 'junit', version: '4.12'

    // https://mvnrepository.com/artifact/org.spockframework/spock-core
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.2-groovy-2.5'
    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-test
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.1.3.RELEASE'


}

test {
    afterTest { desc, result ->
        logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}